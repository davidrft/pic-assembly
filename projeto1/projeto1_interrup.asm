; ***************************************
; PROJETO 1 - MICROCOMPUTADORES         *
;                                       *
; MCU: PIC16F628A	CLOCK: 4MHZ         *
;                                       *
; AUTORES: DAVID RIFF DE F. TENORIO     *
;          DIEGO MAIA HAMILTON          *
;                                       *
; VERSAO: 2.0 (COM INTERRUPÇÃO)         *
; DATA: OUTUBRO DE 2018                 *
; ***************************************

#INCLUDE <P16F628A.INC>

    LIST		P=16F628A

; --- FUSE BITS ---
    __CONFIG     _BOREN_OFF & _CP_OFF & _PWRTE_ON & _WDT_OFF & _LVP_OFF & _MCLRE_ON

; --- PAGINACAO DE MEMORIA ---
#DEFINE     BANK0       BCF     STATUS,RP0
#DEFINE     BANK1       BSF     STATUS,RP0

; --- CONSTANTES ---
DELAY_1S    EQU     D'246'

; --- ENTRADAS ---
#DEFINE     LUMINOSIDADE    PORTB,RB7   ; SENSOR DE LUMINOSIDADE
#DEFINE     FUMACA          PORTB,RB0   ; SENSOR DE FUMA�A
#DEFINE     PARTIDA         PORTB,RB2   ; BOT�O DE PARTIDA
#DEFINE     PRESENCA1       PORTB,RB4   ;
#DEFINE     PRESENCA2       PORTB,RB5   ; PRESENCA NIVEL ALTO -> PRESENCA DETECTADA
#DEFINE     PRESENCA3       PORTB,RB6   ;

; --- SAIDAS ---
#DEFINE     BUZINA          PORTA,RA3   ; SAIDA DO ALARME
#DEFINE     ON              PORTB,RB1   ; SAIDA DE ON/OFF

#DEFINE     LUZ1            PORTA,RA0   ; SAIDA DA LUZ 1
#DEFINE     LUZ2            PORTA,RA1   ; SAIDA DA LUZ 2
#DEFINE     LUZ3            PORTA,RA2   ; SAIDA DA LUZ 3

; --- DEFINICOES GERAIS ---
#DEFINE     THAB        INTCON,TOIE     ; HABILITA INTERRUPCAO
#DEFINE     TFLAG       INTCON,T0IF     ; TIMER OVERFLOW
#DEFINE     EXTFLAG     INTCON,INTF     ; INTERRUPCAO EXTERNA
#DEFINE     RFLAG       INTCON,RBIF     ; INTERRUPCAO DE MUDANCA DE ESTADO RB4 - RB7
#DEFINE     ZERO        STATUS,Z        ; RESULTADO DA ULTIMA OPERACAO FOI 0
#DEFINE     ATIVO1      B'00000001'     ; SENSOR DE PRESENCA 1 ATIVO
#DEFINE     ATIVO2      B'00000010'     ; SENSOR DE PRESENCA 2 ATIVO
#DEFINE     ATIVO3      B'00000100'     ; SENSOR DE PRESENCA 3 ATIVO
#DEFINE     ATIVA_F     B'00001000'     ; SENSOR DE FUMACA 	ATIVO
#

; --- REGISTRADORES DE USO GERAL ---
    CBLOCK 0X20
        W_TEMP
        STATUS_TEMP

        CONTADOR1
		CONTADOR2
		CONTADOR3
		CONTADOR_FUMACA
		PRESENCA_ATIVA
    ENDC

; --- VETOR DE RESET ---
    ORG         H'0000'                 ; ORIGEM NO ENDERECO 0 DE MEMORIA
    GOTO        MAIN                    ; DESVIA DO VETOR DE INTERRUPCAO

; --- VETOR DE INTERRUPCAO ---
    ORG         H'0004'

; --- SALVA CONTEXTO ---
    MOVWF       W_TEMP                  ; COPIA O CONTE�DO DE WORK PARA W_TEMP 
    SWAPF       STATUS,W                ; MOVE O CONTE�DO DE STATUS COM OS NIBBLES INVERTIDOS PARA W
    BANK0                               ; SELECIONA O BANCO 0 DE MEM�RIA
    MOVWF       STATUS_TEMP             ; COPIA O CONTE�DO DE STATUS COM OS NIBBLES INVERTIDOS PARA STATUS_TEMP

; --- TRATAMENTO DA ISR ---
    BTFSC       EXTFLAG                 ; INTERRUPCAO PELO SENSOR DE FUMACA?
    CALL        TEST_FUMACA             

    BTFSC       RFLAG                   ; INTERRUPCAO PELOS SENSORES DE PRESEN�A?
    CALL        CHECK_PRESENCA

    BTFSC       TFLAG                   ; ESTOURO DO TIMER 0?
    CALL        CONTADOR

; --- RECUPERACAO DE CONTEXTO ---
EXIT_ISR:
    SWAPF       STATUS_TEMP,W_TEMP      ; COPIA EM W O CONTEÚDO DE STATUS_TEMP COM OS NIBBLES INVERTIDOS
    MOVWF       STATUS                  ; RECUPERANDO O CONTEÚDO DE STATUS
    SWAPF       W_TEMP,F                ; W_TEMP = W_TEMP COM OS NIBBLES INVERTIDOS
    SWAPF       W_TEMP,W                ; RECUPERA O CONTEÚDO DE WORK

    RETFIE

; --- SUBROTINAS ---
CHECK_PRESENCA:
    BCF         RFLAG
    BTFSS       PRESENCA1               ; CHECA SENSORES DA SECAO 1
    GOTO   	    CHECK2
	BTFSS       LUMINOSIDADE
    GOTO        CHECK2
	BSF         PRESENCA_ATIVA,ATIVO1   ; ATIVA CONTAGEM (LIGA A LUZ) DA SECAO 1
	BSF		    LUZ1
CHECK2:  
	BTFSS       PRESENCA2               ; CHECA SENSORES DA SECAO 2
	GOTO 	    CHECK3
	BTFSS       LUMINOSIDADE
	GOTO	    CHECK3
	BSF         PRESENCA_ATIVA,ATIVO2  ; ATIVA CONTAGEM (LIGA A LUZ) DA SECAO 2
	BSF		    LUZ2
CHECK3:  
	BTFSS       PRESENCA3              ; CHECA SENSORES DA SECAO 3
    GOTO        END_CHECK
	BTFSS       LUMINOSIDADE
    GOTO        END_CHECK
	BSF         PRESENCA_ATIVA,ATIVO3  ; ATIVA CONTAGEM (LIGA A LUZ) DA SECAO 3
	BSF		    LUZ3
END_CHECK:
    RETURN

CONTADOR:
    BCF         TFLAG
	MOVLW		DELAY_1S
	MOVWF		TMR0
TEST_ATIVO1:
	BTFSC       PRESENCA_ATIVA,ATIVO1
    DECFSZ      CONTADOR1,1
    GOTO        TEST_ATIVO2
    CALL        RESET_CONT1
TEST_ATIVO2: 
	BTFSC	    PRESENCA_ATIVA,ATIVO2
    DECFSZ      CONTADOR2,1
    GOTO        TEST_ATIVO3
    CALL        RESET_CONT2
TEST_ATIVO3:
	BTFSC       PRESENCA_ATIVA, ATIVO3
    DECFSZ      CONTADOR3,1
    GOTO        TEST_FUMACA
    CALL        RESET_CONT3
TEST_FUMACA:
    BCF         EXTFLAG
    BTFSS       FUMACA
    GOTO        RESET_CONTF
    DECFSZ      CONTADOR_FUMACA
    GOTO        END_FUMACA
	BSF			BUZINA
RESET_CONTF:
    MOVLW       D'10'
    MOVWF       CONTADOR_FUMACA
    BTFSS       FUMACA
	BCF			BUZINA
END_FUMACA:
    MOVLW       D'8'
    XORWF       CONTADOR_FUMACA, W
    BTFSC       ZERO
    BCF         BUZINA
    RETURN

RESET_CONT1:
    MOVLW       D'60'
    MOVWF 	    CONTADOR1
    BCF 	    PRESENCA_ATIVA,ATIVO1
    BCF 	    LUZ1
    RETURN
RESET_CONT2:
    MOVLW       D'60'
    MOVWF 	    CONTADOR2
    BCF 	    PRESENCA_ATIVA,ATIVO2
    BCF 	    LUZ2
    RETURN
RESET_CONT3:
    MOVLW       D'60'
    MOVWF  	    CONTADOR3
    BCF  	    PRESENCA_ATIVA,ATIVO3
    BCF 	    LUZ3
    RETURN


; --- PROGRAMA PRINCIPAL ---
MAIN:
    BANK1
    MOVLW       B'00000000'     
    MOVWF       TRISA                    ; PORT A COMO SAIDA
    MOVLW       B'11110101'
    MOVWF       TRISB                    ; RB0, RB2, RB4, RB5, RB6 E RB7 COMO ENTRADA
    MOVLW       B'10110001'     
    MOVWF       OPTION_REG               ; DEFINE OPCOES DE OPERACAO PRESCALER 1:4

    MOVLW       B'00000000'
    MOVWF       INTCON                   ; DEFINE OPCOES DE INTERRUPCAO

    MOVLW       B'00001000'
    MOVWF       PCON                     ; UTILIZAR CRISTAL INTERNO DE 4MHZ
    BANK0

    MOVLW       B'00000111'
    MOVWF       CMCON                    ; ENTRADAS ANALOGICAS DESATIVADAS

    MOVLW       B'00000000'
    MOVWF       PORTB                    ; INICIA OUTPUTS EM 0
    MOVLW       B'00000000'
    MOVWF       PORTA                    ; INICIA OUTPUTS EM 0

	MOVLW  	    D'60'
	MOVWF	    CONTADOR1		         ; INICIALIZA CONTADOR DO SENSOR DE PRESENCA1
	MOVLW	    D'60'
	MOVWF	    CONTADOR2		         ; INICIALIZA CONTADOR DO SENSOR DE PRESENCA2
	MOVLW	    D'60'
	MOVWF	    CONTADOR3	             ; INICIALIZA CONTADOR DO SENSOR DE PRESENCA3
    MOVLW	    D'10'
	MOVWF	    CONTADOR_FUMACA	         ; INICIALIZA CONTADOR DO SENSOR DE FUMACA
	MOVLW	    D'0'
	MOVWF	    PRESENCA_ATIVA
	CLRF        TMR0
    BCF         TFLAG
    MOVLW       DELAY_1S
    MOVWF       TMR0

START:
    BTFSS       PARTIDA                  ; VERIFICA SE PARTIDA EST� ATIVADA
    GOTO        START                    ; ESPERA PARTIDA
    BSF         ON                       ; INICIA EXECU��O DO PROGRAMA
    BANK1
    MOVLW       B'10111000'
    MOVWF       INTCON                   ; DEFINE OPCOES DE INTERRUPCAO
    BANK0
    GOTO        $                        ; PERMANECE NESTA LINHA PELO RESTO DA EXECU��O

    END